# Maintainer:  Xiaoxiao Pu <i@xiaoxiao.im>

pkgname=sipvpn-git
pkgver=r4
pkgrel=1
pkgdesc="Simple stateless VPN"
arch=('i686' 'x86_64')
url="https://github.com/XiaoxiaoPu/sipvpn"
license=('GPL3')
depends=('iproute2')
makedepends=('git')
provides=('sipvpn')
source=("git+https://github.com/XiaoxiaoPu/sipvpn.git")
sha256sums=('SKIP')
backup=('etc/sipvpn/hooks/client_down.sh'
        'etc/sipvpn/hooks/client_up.sh'
        'etc/sipvpn/hooks/server_down.sh'
        'etc/sipvpn/hooks/server_up.sh')

pkgver() {
	cd "${srcdir}/sipvpn"
	echo "r$(git rev-list --count HEAD)"
}

build() {
	cd "${srcdir}/sipvpn"
	autoreconf -if
	./configure --prefix=/usr --sysconfdir=/etc
	make
}

package() {
	cd "${srcdir}/sipvpn"
	make DESTDIR="${pkgdir}/" install
	chmod 755 ${pkgdir}/etc/sipvpn/hooks/*
}